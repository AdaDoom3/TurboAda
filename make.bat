@echo off
setlocal
cd /d "%~dp0"

set "SOURCE=turboada.c"
set "RUNTIME=turboada-runtime.ada"
set "LEGACY=turboada-runtime-legacy.ada"
set "BUNDLE=turboada-extension.html"
set "MANUAL=turboada-manual.md"
set "VSIX=turboada.vsix"
set "ICON=turboada-icon"
set "LIBRARIES=bin-libraries.zip"
set "STAGE=bin-windows"
set "ZIG_VERSION=0.16.0"
set "ZIG_NAME=zig-x86_64-windows-%ZIG_VERSION%"
set "ZIG_URL=https://ziglang.org/download/%ZIG_VERSION%/%ZIG_NAME%.zip"

set "TOOLCHAIN_windows=GCC, Clang or Zig"
set "TOOLCHAIN_linux=Zig"
set "TOOLCHAIN_macos=Zig and llvm-lipo"
set "EXECUTABLE_windows=ta.exe"
set "EXECUTABLE_linux=ta"
set "EXECUTABLE_macos=ta"
set "COMPILER_FLAGS_windows=-O2 -Wall -g0 -std=gnu2x -static"
set "COMPILER_FLAGS_linux=-O3 -Wall -g0 -std=gnu17 -mcpu=baseline"
set "COMPILER_FLAGS_macos=-O3 -Wall -g0 -std=gnu2x"
set "LINK_LIBRARIES_windows="
set "LINK_LIBRARIES_linux="
set "LINK_LIBRARIES_macos="
set "ARTWORK_windows=%ICON%.ico"
set "ARTWORK_linux=%ICON%.png"
set "ARTWORK_macos=%ICON%.icns"
set "ICON_SOURCE=%ICON%.png"
set "ARCHITECTURES_linux=x86_64-linux-gnu.2.34"
set "ARCHITECTURES_macos=aarch64-macos x86_64-macos"
set "LAUNCHER_linux=turboada.desktop"
set "SHARED_LIBRARIES_windows=*.dll"

call :dispatch %1 %2
set "RESULT=%errorlevel%"
echo %cmdcmdline% | "%SystemRoot%\System32\find.exe" /i "/c" >nul && pause
exit /b %RESULT%

:dispatch
if /i "%~1"=="/?"      goto usage
if /i "%~1"=="-h"      goto usage
if /i "%~1"=="help"    goto usage
if /i "%~1"=="clean"   goto clean
if /i "%~1"=="package" goto package
if /i "%~1"=="vsix"    goto vsix
if "%~1"=="" goto make
echo Invalid parameter - %~1
call :usage
exit /b 1

:usage
echo Makes the Ada 83 compiler.
echo.
echo MAKE [command] [target]
echo.
echo   clean      Deletes the bin-^<target^> folders, any Zig, and the leftovers
echo              of older builds that wrote to this folder.
echo   package    Makes the compiler and %VSIX%, filling bin-^<target^> with
echo              everything a release carries.
echo   vsix       Makes %VSIX%, the VS Code extension, only.
echo   help       Displays this help.
echo.
echo   windows    Packages for this machine with %TOOLCHAIN_windows%. The default.
echo   linux      Cross-packages with %TOOLCHAIN_linux%.
echo   macos      Cross-packages with %TOOLCHAIN_macos%.
echo.
echo Everything built lands in bin-^<target^>, so bin-windows\%EXECUTABLE_windows%
echo with no command. Cross-packaging downloads nothing; install the toolchain
echo it names first.
echo.
echo LLVM-C.dll, the official Windows build from the llvm-project release, is
echo unpacked from %LIBRARIES% and must stay with %EXECUTABLE_windows%, which
echo loads it when it runs; it needs nothing but Windows itself.
exit /b 0

:select
set "TARGET=%~1"
if not defined TARGET set "TARGET=windows"
if not defined EXECUTABLE_%TARGET% (
    echo Target is '%TARGET%'; it must be linux, macos or windows.
    exit /b 1
)
call set "EXECUTABLE=%%EXECUTABLE_%TARGET%%%"
call set "COMPILER_FLAGS=%%COMPILER_FLAGS_%TARGET%%%"
call set "LINK_LIBRARIES=%%LINK_LIBRARIES_%TARGET%%%"
call set "ARTWORK=%%ARTWORK_%TARGET%%%"
call set "ARCHITECTURES=%%ARCHITECTURES_%TARGET%%%"
call set "LAUNCHER=%%LAUNCHER_%TARGET%%%"
call set "SHARED_LIBRARIES=%%SHARED_LIBRARIES_%TARGET%%%"
set "STAGE=bin-%TARGET%"
if not exist "%STAGE%" mkdir "%STAGE%"
exit /b 0

:clean
del /q "%EXECUTABLE_windows%" ta.pdb ta.obj LLVM-C.dll ^
    zig.zip "%VSIX%" icon.rc icon.res icon.res.o >nul 2>nul
for %%D in (staging builds zig bin-linux bin-macos bin-windows) do rmdir /s /q %%D >nul 2>nul
echo Cleaned.
exit /b 0

:make
call :select   || exit /b 1
call :icons
call :native   || exit /b 1
call :stage    || exit /b 1
echo Built %STAGE%\%EXECUTABLE%.
exit /b 0

:package
call :select "%~2" || exit /b 1
rmdir /s /q "%STAGE%" >nul 2>nul
mkdir "%STAGE%"
call :vsix  || exit /b 1
call :icons || (
    echo Cannot build the icons. Making archives needs PowerShell 5 or later.
    exit /b 1
)
if defined ARCHITECTURES (
    call :cross || exit /b 1
) else (
    call :native || exit /b 1
)
call :stage   || exit /b 1
rmdir /s /q staging >nul 2>nul
call :archive || exit /b 1
exit /b 0

:archive
if not exist builds mkdir builds
set "ARCHIVE=builds\bin-%TARGET%.zip"
del /q "%ARCHIVE%" >nul 2>nul
powershell -NoProfile -Command "Compress-Archive -Path '%STAGE%\*' -DestinationPath '%ARCHIVE%' -Force" || (
    echo Cannot pack %ARCHIVE%. Making archives needs PowerShell 5 or later.
    exit /b 1
)
rmdir /s /q staging\proof >nul 2>nul
powershell -NoProfile -Command "Expand-Archive -Path '%ARCHIVE%' -DestinationPath 'staging\proof' -Force" || exit /b 1
if /i "%TARGET%"=="windows" (
    if not exist "staging\proof\LLVM-C.dll" (
        echo %ARCHIVE% does not carry LLVM-C.dll.
        exit /b 1
    )
    for %%D in (libwinpthread libgcc_s libstdc++ libiconv libxml2 libzstd zlib1) do if exist "staging\proof\%%D*.dll" (
        echo %ARCHIVE% carries a MinGW runtime DLL, %%D.
        exit /b 1
    )
    call :prove || exit /b 1
) else (
    echo %ARCHIVE% was built for %TARGET% and cannot run here.
)
rmdir /s /q staging\proof >nul 2>nul
echo Packaged %ARCHIVE%.
exit /b 0

:prove
call :version || exit /b 1
"staging\proof\ta.exe" --version | "%SystemRoot%\System32\findstr.exe" /x /c:"ta %VERSION%" >nul || (
    echo The packaged compiler does not answer 'ta %VERSION%'; it answered:
    "staging\proof\ta.exe" --version
    exit /b 1
)
> "staging\proof\hello.adb" (
    echo with Text_IO;
    echo procedure Hello is
    echo begin
    echo   Text_IO.Put_Line ^("packaged ta works"^);
    echo end Hello;
)
pushd staging\proof
ta.exe hello.adb -o hello || (
    popd
    exit /b 1
)
hello.exe | "%SystemRoot%\System32\findstr.exe" /x /c:"packaged ta works" >nul || (
    popd
    echo The packaged compiler cannot build and run a program.
    exit /b 1
)
popd
> "staging\proof\legacy.adb" (
    echo with Ada.Strings.Fixed, Text_IO;
    echo procedure Legacy is
    echo begin
    echo   Text_IO.Put_Line ^(Ada.Strings.Fixed.Trim ^("  legacy units served  ", Ada.Strings.Both^)^);
    echo end Legacy;
)
pushd staging\proof
ta.exe legacy.adb -o legacy || (
    popd
    exit /b 1
)
legacy.exe | "%SystemRoot%\System32\findstr.exe" /x /c:"legacy units served" >nul || (
    popd
    echo The packaged compiler cannot serve %LEGACY%.
    exit /b 1
)
popd
exit /b 0

:version
set "MAJOR="
set "MINOR="
for /f "tokens=3" %%V in ('%SystemRoot%\System32\findstr.exe /r /c:"^#define TURBOADA_VERSION_MAJOR" %SOURCE%') do set "MAJOR=%%V"
for /f "tokens=3" %%V in ('%SystemRoot%\System32\findstr.exe /r /c:"^#define TURBOADA_VERSION_MINOR" %SOURCE%') do set "MINOR=%%V"
set "VERSION=%MAJOR%.%MINOR%"
if not defined MAJOR goto version_missing
if not defined MINOR goto version_missing
exit /b 0
:version_missing
echo Cannot read TURBOADA_VERSION_MAJOR/_MINOR from %SOURCE%.
exit /b 1

:native
call :require %SOURCE%  || exit /b 1
call :require %RUNTIME% || exit /b 1
call :require %LEGACY%  || exit /b 1
call :unpack_llvm       || exit /b 1
call :compile           || exit /b 1
"%STAGE%\%EXECUTABLE%" --version >nul 2>nul || (
    echo %STAGE%\%EXECUTABLE% was built but does not run.
    exit /b 1
)
exit /b 0

:cross
call :require %SOURCE%  || exit /b 1
call :require %RUNTIME% || exit /b 1
call :require %LEGACY%  || exit /b 1
call :find_zig || (
    echo packaging for %TARGET% needs zig
    exit /b 1
)
set "SLICES="
set "MANY_SLICES="
for %%A in (%ARCHITECTURES%) do (
    call :slice %%A || exit /b 1
)
if defined MANY_SLICES (
    call :lipo || exit /b 1
) else (
    move /y %SLICES% "%STAGE%\%EXECUTABLE%" >nul
)
exit /b 0

:slice
set "SLICE=%STAGE%\%EXECUTABLE%-%~1"
echo   compiling for %~1 with Zig
%ZIG% cc %COMPILER_FLAGS% -target %~1 -o %SLICE% %SOURCE% %LINK_LIBRARIES%
if errorlevel 1 exit /b 1
if defined SLICES set "MANY_SLICES=1"
set "SLICES=%SLICES% %SLICE%"
exit /b 0

:lipo
set "LIPO=lipo"
where lipo >nul 2>nul && goto join
set "LIPO=llvm-lipo"
where llvm-lipo >nul 2>nul && goto join
goto fatjoin
:join
%LIPO% -create -output "%STAGE%\%EXECUTABLE%" %SLICES%
if errorlevel 1 exit /b 1
del /q %SLICES% >nul 2>nul
exit /b 0

:fatjoin
echo   joining the %TARGET% slices into a universal binary
powershell -NoProfile -Command ^
    "$ErrorActionPreference='Stop';" ^
    "$Big = { param($v) [byte[]]@((($v -shr 24) -band 255),(($v -shr 16) -band 255)," ^
    "                             (($v -shr 8) -band 255),($v -band 255)) };" ^
    "$Names = '%SLICES%'.Trim() -split '\s+';" ^
    "$Align = 16384; $Offset = $Align; $Entries = @(); $Bodies = @();" ^
    "foreach ($Name in $Names) {" ^
    "  $Bytes = [IO.File]::ReadAllBytes((Join-Path $PWD $Name));" ^
    "  $Cpu = [BitConverter]::ToUInt32($Bytes, 4); $Sub = [BitConverter]::ToUInt32($Bytes, 8);" ^
    "  $Entries += ,@($Cpu, $Sub, $Offset, $Bytes.Length); $Bodies += ,$Bytes;" ^
    "  $Offset = ($Offset + $Bytes.Length + $Align - 1) -band (-bnot ($Align - 1)) };" ^
    "$Out = (& $Big 3405691582) + (& $Big $Names.Count);" ^
    "foreach ($e in $Entries) {" ^
    "  $Out += (& $Big $e[0]) + (& $Big $e[1]) + (& $Big $e[2]) + (& $Big $e[3]) + (& $Big 14) };" ^
    "for ($i = 0; $i -lt $Bodies.Count; $i++) {" ^
    "  $Out += [byte[]]::new($Entries[$i][2] - $Out.Length) + $Bodies[$i] };" ^
    "[IO.File]::WriteAllBytes((Join-Path $PWD '%STAGE%\%EXECUTABLE%'), $Out)"
if not exist "%STAGE%\%EXECUTABLE%" (
    echo Cannot join the %TARGET% slices. Install lipo or llvm-lipo and try again.
    exit /b 1
)
del /q %SLICES% >nul 2>nul
exit /b 0

:stage
copy /y "%RUNTIME%" "%STAGE%\" >nul
copy /y "%LEGACY%" "%STAGE%\" >nul
if defined LAUNCHER call :launcher
exit /b 0

:icons
if not exist "%ICON_SOURCE%" exit /b 1
if not exist "%STAGE%" mkdir "%STAGE%"
echo   building the %TARGET% icons
powershell -NoProfile -Command ^
    "$ErrorActionPreference='Stop';" ^
    "$Png = [IO.File]::ReadAllBytes((Join-Path $PWD '%ICON_SOURCE%'));" ^
    "$Big = { param($v) [byte[]]@((($v -shr 24) -band 255),(($v -shr 16) -band 255)," ^
    "                             (($v -shr 8) -band 255),($v -band 255)) };" ^
    "$Little = { param($v) [byte[]]@(($v -band 255),(($v -shr 8) -band 255)," ^
    "                                (($v -shr 16) -band 255),(($v -shr 24) -band 255)) };" ^
    "$Wide = ($Png[16] -shl 24) + ($Png[17] -shl 16) + ($Png[18] -shl 8) + $Png[19];" ^
    "$Tall = ($Png[20] -shl 24) + ($Png[21] -shl 16) + ($Png[22] -shl 8) + $Png[23];" ^
    "$Chunk = @{16='icp4';32='icp5';64='icp6';128='ic07';256='ic08';512='ic09';1024='ic10'}[$Wide];" ^
    "if (-not $Chunk) { $Chunk = 'ic07' };" ^
    "$Stage = Join-Path $PWD '%STAGE%';" ^
    "$Ico = [byte[]]@(0,0,1,0,1,0,($Wide -band 255),($Tall -band 255),0,0,1,0,32,0) +" ^
    "       (& $Little $Png.Length) + (& $Little 22) + $Png;" ^
    "if ('%TARGET%' -eq 'windows') {" ^
    "  [IO.File]::WriteAllBytes((Join-Path $Stage '%ICON%.ico'), $Ico) };" ^
    "$Icns = [Text.Encoding]::ASCII.GetBytes('icns') + (& $Big ($Png.Length + 16)) +" ^
    "        [Text.Encoding]::ASCII.GetBytes($Chunk) + (& $Big ($Png.Length + 8)) + $Png;" ^
    "if ('%TARGET%' -eq 'macos') {" ^
    "  [IO.File]::WriteAllBytes((Join-Path $Stage '%ICON%.icns'), $Icns) };" ^
    "if ('%TARGET%' -eq 'linux') {" ^
    "  [IO.File]::WriteAllBytes((Join-Path $Stage '%ICON%.png'), $Png) };" ^
    "$Data = $Icns.Length + 4;" ^
    "$Map = [byte[]]::new(24) + [byte[]]@(0,28,0,46,0,0) +" ^
    "       [Text.Encoding]::ASCII.GetBytes('icns') + [byte[]]@(0,0,0,10,191,185,255,255,0,0,0,0);" ^
    "$Fork = (& $Big 256) + (& $Big (256 + $Data)) + (& $Big $Data) + (& $Big 46) +" ^
    "        [byte[]]::new(240) + (& $Big $Icns.Length) + $Icns + $Map;" ^
    "$Double = (& $Big 333319) + (& $Big 131072) + [byte[]]::new(16) + [byte[]]@(0,2) +" ^
    "          (& $Big 9) + (& $Big 50) + (& $Big 32) +" ^
    "          (& $Big 2) + (& $Big 82) + (& $Big $Fork.Length) +" ^
    "          [byte[]]::new(8) + [byte[]]@(4,0) + [byte[]]::new(22) + $Fork;" ^
    "if ('%TARGET%' -eq 'macos') {" ^
    "  New-Item -ItemType Directory -Force -Path (Join-Path $Stage '__MACOSX') > $null;" ^
    "  [IO.File]::WriteAllBytes((Join-Path $Stage '__MACOSX\._ta'), $Double) }"
if exist "%STAGE%\%ARTWORK%" exit /b 0
exit /b 1

:launcher
powershell -NoProfile -Command ^
    "$ErrorActionPreference='Stop';" ^
    "$Entry = '[Desktop Entry]','Type=Application','Name=Ada 83'," ^
    "         'Comment=Ada 83 compiler','Exec=ta %%F','Icon=%ICON%'," ^
    "         'Terminal=true','Categories=Development;Building;';" ^
    "[IO.File]::WriteAllText((Join-Path $PWD '%STAGE%\%LAUNCHER%')," ^
    "                        ($Entry -join [char]10) + [char]10)"
if exist "%STAGE%\%LAUNCHER%" exit /b 0
echo Cannot write %LAUNCHER%. Making archives needs PowerShell 5 or later.
exit /b 1

:vsix
if not defined TARGET (
    call :select || exit /b 1
)
call :require %BUNDLE% || exit /b 1
del /q "%STAGE%\%VSIX%" >nul 2>nul
rmdir /s /q staging\vsix >nul 2>nul
mkdir staging\vsix\extension\syntaxes
for %%F in ("%MANUAL%" "%ICON%.png" turboada-logo.png) do (
    if exist %%F ( copy /y %%F staging\vsix\extension\ >nul ) else (
        echo %%~F is missing; building %VSIX% without it.
    )
)
echo   splitting %BUNDLE%
call :split
echo   packing %VSIX%
powershell -NoProfile -Command ^
    "$ErrorActionPreference='Stop';" ^
    "$Parts = Get-ChildItem -LiteralPath 'staging\vsix' -Force | ForEach-Object FullName;" ^
    "Compress-Archive -LiteralPath $Parts -DestinationPath 'staging\vsix.zip' -Force;" ^
    "Move-Item 'staging\vsix.zip' '%STAGE%\%VSIX%' -Force"
rmdir /s /q staging\vsix >nul 2>nul
if not exist "%STAGE%\%VSIX%" (
    echo Cannot write %VSIX%. Making archives needs PowerShell 5 or later.
    exit /b 1
)
echo Built %STAGE%\%VSIX%.
exit /b 0

:split
setlocal disabledelayedexpansion
set "OUT="
for /f "delims=" %%L in ('findstr /n "^" "%BUNDLE%"') do (
    set "LINE=%%L"
    setlocal enabledelayedexpansion
    set "LINE=!LINE:*:=!"
    set "TAG="
    if "!LINE:~0,7!"=="<script" set "TAG=1"
    if "!LINE!"=="</script>" set "TAG=1"
    if defined TAG (
        endlocal
        set "OUT="
        for /f tokens^=2^ delims^=^" %%N in ("%%L") do call :destination "%%N"
    ) else (
        if defined OUT >>"!OUT!" echo(!LINE!
        endlocal
    )
)
endlocal
exit /b 0

:destination
set "NAME=%~1"
set "OUT=staging\vsix\extension\%NAME%"
if "%NAME:~0,22%"=="extension.vsixmanifest" set "OUT=staging\vsix\%NAME%"
if "%NAME:~0,1%"=="[" set "OUT=staging\vsix\%NAME%"
type nul >"%OUT%"
exit /b 0

:require
if exist "%~1" exit /b 0
echo Cannot find %~1. Run this script from the folder it came in.
exit /b 1

:unpack_llvm
if exist "%STAGE%\LLVM-C.dll" exit /b 0
if not exist "%LIBRARIES%" (
    echo Cannot find LLVM-C.dll or %LIBRARIES%.
    exit /b 1
)
echo   unpacking LLVM-C.dll into %STAGE%
powershell -NoProfile -Command ^
    "$ErrorActionPreference='Stop';" ^
    "Expand-Archive -LiteralPath '%LIBRARIES%' -DestinationPath 'staging\llvm' -Force;" ^
    "Move-Item 'staging\llvm\*.dll' '%STAGE%' -Force"
rmdir /s /q staging\llvm >nul 2>nul
if exist "%STAGE%\LLVM-C.dll" exit /b 0
echo Cannot unpack %LIBRARIES%. Extract the DLLs into %STAGE% by hand and try again.
exit /b 1

:compile
set "TOOLCHAIN=GCC"
set "COMPILER=gcc"
where gcc >nul 2>nul && goto build
set "TOOLCHAIN=Clang"
set "COMPILER=clang --target=x86_64-w64-windows-gnu"
where clang >nul 2>nul && goto build
where clang-cl >nul 2>nul && (call :compile_windows_native && exit /b 0)
call :offer_zig || exit /b 1
set "TOOLCHAIN=Zig"
set "COMPILER=%ZIG% cc -target x86_64-windows-gnu"
:build
call :resource
echo   compiling turboada.c with %TOOLCHAIN%
%COMPILER% %COMPILER_FLAGS% %SOURCE% %RESOURCE% -o "%STAGE%\%EXECUTABLE%" %LINK_LIBRARIES%
exit /b %errorlevel%

:compile_windows_native
set "RESDIR="
for /f "delims=" %%R in ('clang-cl -print-resource-dir 2^>nul') do set "RESDIR=%%R"
if not defined RESDIR exit /b 1
set "BUILTINS=%RESDIR%\lib\windows\clang_rt.builtins-x86_64.lib"
if not exist "%BUILTINS%" exit /b 1
call :resource
echo   compiling turboada.c with clang-cl (windows-native)
clang-cl /nologo /O2 /clang:-std=gnu2x -fuse-ld=lld /Fe:"%STAGE%\%EXECUTABLE%" ^
    %SOURCE% %RESOURCE% "%BUILTINS%"
set "RC=%errorlevel%"
del /q ta.obj >nul 2>nul
exit /b %RC%

:resource
set "RESOURCE=icon.res.o"
if defined ZIG set "RESOURCE=icon.res"
del /q icon.rc "%RESOURCE%" >nul 2>nul
if not exist "%STAGE%\%ICON%.ico" call :icons
set "ICON_PATH=%STAGE%/%ICON%.ico"
set "ICON_PATH=%ICON_PATH:\=/%"
if exist "%STAGE%\%ICON%.ico" >icon.rc echo 1 ICON "%ICON_PATH%"
if exist icon.rc if defined ZIG %ZIG% rc icon.rc "%RESOURCE%" >nul 2>nul
if exist icon.rc if not defined ZIG windres icon.rc -O coff -o "%RESOURCE%" >nul 2>nul
del /q icon.rc >nul 2>nul
if exist "%RESOURCE%" exit /b 0
set "RESOURCE="
echo Building without the icon; %ARTWORK% could not be compiled in.
exit /b 0

:find_zig
set "ZIG=zig"
where zig >nul 2>nul && exit /b 0
set "ZIG=zig\zig.exe"
if exist "%ZIG%" exit /b 0
set "ZIG="
exit /b 1

:offer_zig
call :find_zig && exit /b 0
echo No C compiler was found. Zig %ZIG_VERSION% is one download that builds %SOURCE%.
set /p "REPLY=Download it now? [y/N] "
if /i not "%REPLY%"=="y" (
    echo Install MinGW-w64 GCC, Clang or Zig and run this again.
    exit /b 1
)
powershell -NoProfile -Command ^
    "$ErrorActionPreference='Stop';" ^
    "Invoke-WebRequest '%ZIG_URL%' -OutFile 'zig.zip';" ^
    "Expand-Archive 'zig.zip' '.' -Force;" ^
    "Move-Item '%ZIG_NAME%' 'zig' -Force;" ^
    "Remove-Item 'zig.zip'"
set "ZIG=zig\zig.exe"
if exist "%ZIG%" exit /b 0
set "ZIG="
echo Cannot download Zig. Install MinGW-w64 GCC or Clang and try again.
exit /b 1
